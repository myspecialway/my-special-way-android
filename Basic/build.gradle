apply plugin: 'com.android.application'


android {
    compileSdkVersion 23
    buildToolsVersion '27.0.3'

    // note: use gradle.properties in project root level to set your api credentials
    def apiKey = project.properties['indoorAtlasApiKey'] ?: "api-key-not-set";
    def apiSecret = project.properties['indoorAtlasApiSecret'] ?: "api-secret-not-set";
    def pubNubPublishKey = project.properties['pubNubPublishKey'] ?: "not-set";
    def pubNubSubscribeKey = project.properties['pubNubSubscribeKey'] ?: "not-set";


    defaultConfig {
        applicationId "org.myspecialway.examples"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        // avoid getting errors from malformed string resources if secret contains '%' chars
        resValue "string", "indooratlas_api_key", apiKey
        resValue "string", "indooratlas_api_secret", apiSecret.replaceAll("%", "\\\\u0025")
        resValue "string", "pubnub_publish_key", pubNubPublishKey
        resValue "string", "pubnub_subscribe_key", pubNubSubscribeKey
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "sdk_debug"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            debuggable true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation "com.indooratlas.android:indooratlas-android-sdk:2.7.0-beta-848@aar"
    implementation 'com.indooratlas.android:indooratlas-android-wayfinding:2.7.0-beta-9'

    implementation 'com.android.support:appcompat-v7:23.2.1'
    implementation 'com.google.android.gms:play-services-maps:8.1.0'
    implementation 'com.google.maps.android:android-maps-utils:0.3.+'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.2.0'
    implementation 'com.pubnub:pubnub-android:3.7.5'
    implementation 'com.android.support:design:23.2.1'

    // for Open Street Map support
    implementation 'org.osmdroid:osmdroid-android:5.6.4'
    implementation 'com.github.MKergall:osmbonuspack:6.2'
}
